Class BD_SuperShotgun : BDoomWeapon replaces Supershotgun
{
bool ShotFired;
Default
	{
	Tag "$TAG_SUPERSHOTGUN";
	weapon.selectionorder 40;
	Weapon.AmmoType2	"Shell";
	Weapon.AmmoGive2	8;
	weapon.slotnumber 3;
	weapon.slotpriority 1;
	}
override string PickupMessage ()
	{
	if(bdoom_weapons == 2) 
		return "You got the Riot Gun!"; 
	else
		return "$GOTSHOTGUN2"; 
	}
override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
    {
	if(bdoom_weapons == 2) 
		return "%o was blasted by %k's riot gun."; 
	else 
		return "$OB_MPSSHOTGUN"; 
    }
override void Tick()
	{
	If (Owner)
		return;
	else
		{
		if(bdoom_weapons == 2)
			{
			pickupsound = "Mweapons/assg/clipin";
			AmmoGive1 = 0;
			if (bDROPPED == true)
				AmmoGive2 = 4;
			else
				AmmoGive2 = 8;
			}
		else
			{
			pickupsound = "weapons/ssg/close";
			AmmoGive2 = 0;
			if (bDROPPED == true)
				AmmoGive1 = 4;
			else
				AmmoGive1 = 8;
			}
		}
	Super.Tick();
	}
override void DoEffect()
	{
	if (owner)
		{
		if(bdoom_weapons == 2)
			{
			Ammo1 = ammo(Owner.FindInventory("SSGClip") );
			ammouse1 = 2;
			Ammo2 = ammo(Owner.FindInventory("Shell"));
			ammouse2 = 0;
			bNOAUTOFIRE = true;
			bAMMO_OPTIONAL = true;
			}
		if(bdoom_weapons == 1)
			{
			Ammo1 = ammo(Owner.FindInventory("Shell"));
			ammouse1 = 2;
			Ammo2 = ammo(Owner.FindInventory("Shell"));
			ammouse2 = 1;
			bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = false;
			}
		if(bdoom_weapons == 0)
			{
			Ammo1 = ammo(Owner.FindInventory("Shell"));
			ammouse1 = 2;
			Ammo2 = null;
			ammouse2 = 0;
			bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = false;
			}
		}
	Super.DoEffect();
	}
states
{
	Spawn:
		SGN2 A 1 NoDelay A_JumpIf(bdoom_weapons == 2,1);
		loop;
		ASSG Z 1;
		loop;
	Select:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Select.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Select.Enhanced");
		//TNT1 AAAAAAAA 0 A_Raise();
		SH21 ABCDEF 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Deselect:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"DeSelect.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Deselect.Enhanced");
		SH2N C 1 A_Lower();
		wait;
	Ready:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Ready.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Ready.Enhanced");
		SH2N C 1 A_WeaponReady(WRF_NOSECONDARY);
		loop;
	BarrelSmoke:
		TNT1 A random(15,20);
		TNT1 AAAAAAAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Enhanced"))	||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*12,0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	BarrelSmoke1:
		TNT1 A random(45,50);
		TNT1 AAAAAAAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Enhanced"))	||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*12,-0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	BarrelSmoke2:
		TNT1 A random(45,50);
		TNT1 AAAAAAAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Enhanced"))	||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	) {
				A_Eject("BarrelSmoke",cos(pitch)*12,0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*12,-0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				}
			}
		stop;
	Fire:
	//fire = 10 tics, reload = 47 tics, refire 5 tics
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Fire.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Fire.Enhanced");
		SH2N C 3 A_Gunflash();
		SH2F Z 2 {
				A_PlaySound("weapons/sshotf");
				if (bdoom_tracers == 1)
					A_FireBullets(11.2, 7.1, 20, 5, "EnShotPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0);
				else
					A_FireBullets(11.2, 7.1, 20, 5, "EnShotPuff",FBF_NORANDOMPUFFZ);
				}
		SH2F A 2;
		SH2F B 2;
		SH2N C 1 { 
			If(bdoom_debris==1)	{
				A_Overlay(10,"BarrelSmoke2");
				}
			}
		TNT1 A 0 A_CheckReload();
	Reload:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Reload.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Reload.Enhanced");
		SH2R ABCDEFGH 1;
		SH2R I 1 A_playsound("weapons/sshoto");
		SH2R JKL 1;
		TNT1 A 0 { 
			if (Bdoom_debris ==1) {
				A_Eject("ShotgunCasing",16,-3.5,6.5,frandom(0,1),FRandom(-2.5,-3),frandom(4,5));
				A_Eject("ShotgunCasing",16,-5,6.5,frandom(0,1),FRandom(-2.5,-3),frandom(4,5));
				}
			}
		SH2R MNOPQX 2;
		SH2R Y 2 A_playsound("weapons/sshotl");
		SH2R Z 2;
		SH2S ABCD 1;
		SH2S EFGH 1;
		TNT1 A 0 A_playsound("weapons/sshotc");
		SH2S H 2;
		SH2N C 6 A_Refire();
		SH2N C 5 A_Refire();
		goto ready	;
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		SH2L A 3 bright A_Light1();
		SH2L B 2 bright A_light2();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5);
		SH2P A 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,5);
		SH2P B 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,3);
		SH2P C 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,1,2,3);
		SH2P D 2 bright A_Light2();
		goto lightdone;
		SH2P E 2 bright A_Light2();
		goto lightdone;
		SH2P F 2 bright A_Light2();
		goto lightdone;
	Select.Enhanced:
		TNT1 A 0 A_PlaySound("weapons/ssg/draw");
		TNT1 A 0 A_JumpIf(invoker.ShotFired == true,"Select1");
		//TNT1 AAAAAAAA 0 A_Raise();
		SH21 ABCDE 1 A_WeaponReady(WRF_NOFIRE);
		SH21 F 1 {
			A_WeaponReady(WRF_NOFIRE);
			let s = player.FindPSprite (PSP_Weapon);
			if (invoker.ShotFired) {
				s.sprite = GetSpriteIndex("SH2N");
				s.frame = 1;
				}
			}
		goto ready;
	Deselect.Enhanced:
		SH2N A 0 {
			let s = player.FindPSprite (PSP_Weapon);
			if (invoker.ShotFired)
				s.frame = 1;
			else
				s.frame = 0;
			}
		#### # 1 A_Lower(12);
		wait;
	Ready.Enhanced:
		SH2N A 1 {
			A_WeaponReady();
			let s = player.FindPSprite (PSP_Weapon);
			if (invoker.ShotFired)
				s.frame = 1;
			else
				s.frame = 0;
			}
		goto ready;
	Fire.Enhanced:
		TNT1 A 0 A_JumpIf(invoker.ShotFired == true,"Reload.Enhanced1");
		SH2N A 3 A_Gunflash();
		SH2F X 1 {
				A_PlaySound("weapons/ssg/fire",5);
				if (bdoom_tracers == 1)
					A_FireBullets(11.2, 7.1, 20, 5, "EnShotPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0);
				else
					A_FireBullets(11.2, 7.1, 20, 5, "EnShotPuff",FBF_NORANDOMPUFFZ);
				}
		
		SH2F X 1 A_Playsound("weapons/ssg/fire", 6);
		SH2F Y 1;
		SH2F AB 1;
		SH2N C 2 { 
			If(bdoom_debris==1)	{
				A_Overlay(10,"BarrelSmoke2");
				}
			}
		TNT1 A 0 A_CheckReload();
		goto reload;
	AltFire:
		TNT1 A 0 A_JumpIf(invoker.ShotFired == true,"AltFireSecond");
		SH2N A 3 A_Gunflash("AltFlash.Right");

		SH2F X 1 {
				invoker.ShotFired = true;
				A_PlaySound("weapons/ssg/fire",5);
				if (bdoom_tracers == 1)
					A_FireBullets(10,6.5,10,5, "EnShotPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,1);
				else
					A_FireBullets(10,6.5,10,5,"EnShotPuff",FBF_NORANDOMPUFFZ);
				}
		TNT1 A 0 { 
			If(bdoom_debris==1)				
				A_Overlay(10,"BarrelSmoke");
			}
		SH2F CD 2;
		SH2N B 2;
		goto ready;
	AltFireSecond:
		SH2N B 3 A_Gunflash("AltFlash.Left");
		SH2F Y 1 {
				invoker.ShotFired = false;
				A_PlaySound("weapons/ssg/fire",5);
				if (bdoom_tracers == 1)
					A_FireBullets(10,6.5,10,5, "EnShotPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,1);
				else
					A_FireBullets(10,6.5,10,5,"EnShotPuff",FBF_NORANDOMPUFFZ);
				}
		TNT1 A 0 { 
			If(bdoom_debris==1)	{
				let s = player.FindPSprite (10);
				if (s) //check if it's currently trying to show smoke over the right barrel (i.e. if two shots were fired in quick succession)
					A_Overlay(10,"BarrelSmoke2"); //if true, show the double smoke animation
				else
					A_Overlay(11,"BarrelSmoke1"); //otherwise spawn smoke over the left barrel only (if there was a long pause between shots)
				}
			}
		SH2F AB 2;
		SH2N C 2;	
		TNT1 A 0 A_CheckReload();
		goto Reload.Enhanced;
	Reload.Enhanced:
		SH2R ABCDEFGH 1;
		SH2R I 1 A_playsound("weapons/ssg/open");
		SH2R JKL 1;
		TNT1 A 0 { 
			if (Bdoom_debris ==1) {
				A_Eject("ShotgunCasing",16,-3.5,6.5,frandom(0,1),FRandom(-2.5,-3),frandom(4,5));
				A_Eject("ShotgunCasing",16,-5,6.5,frandom(0,1),FRandom(-2.5,-3),frandom(4,5));
				}
			}
		SH2R MNOPQX 2;
		SH2R Y 2 A_playsound("weapons/ssg/in");
		SH2R Z 2;
		SH2S ABCD 1;
		SH2S EFGH 1;
		TNT1 A 0 A_playsound("weapons/ssg/close");
		SH2S H 2;
		SH2N A 6 A_Refire();
		SH2N A 5 A_Refire();
		goto ready	;
	Reload.Enhanced1:
		TNT1 A 0 A_CheckReload();
		TNT1 A 0 { invoker.ShotFired = false; }
		SH2R ABCDEFGH 1;
		SH2R I 1 A_playsound("weapons/ssg/open");
		SH2R JKL 1;
		TNT1 A 0 { 
			If(Bdoom_debris == 1)
				A_Eject("ShotgunCasing",16,-5,6.5,frandom(0,1),FRandom(-2.5,-3),frandom(4,5));
			}
		SH2R UVWX 2;
		SH2R Y 2 A_playsound("weapons/ssg/in");
		SH2R Z 1;
		SH2S ABCDEFG 1;
		TNT1 A 0 A_playsound("weapons/ssg/close");
		SH2S H 1 A_ReFire();
		SH2N A 5 A_Refire();
		goto ready.Enhanced;
	AltFlash.Right:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		SH2L E 3 bright A_Light1();
		SH2L F 2 bright A_light2();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5);
		SH2O A 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,5);
		SH2O B 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,3);
		SH2O C 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,1,2,3);
		SH2O D 2 bright A_Light2();
		goto lightdone;
		SH2O E 2 bright A_Light2();
		goto lightdone;
		SH2O F 2 bright A_Light2();
		goto lightdone;
	AltFlash.Left:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		SH2L C 3 bright A_Light1();
		SH2L D 2 bright A_light2();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5);
		SH2M A 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,5);
		SH2M B 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,3);
		SH2M C 3 bright A_Light1();
		TNT1 A 0 A_Jump(256,1,2,3);
		SH2M D 2 bright A_Light2();
		goto lightdone;
		SH2M E 2 bright A_Light2();
		goto lightdone;
		SH2M F 2 bright A_Light2();
		goto lightdone;
	Select.Modern:
		TNT1 A 0 A_PlaySound("weapons/ssg/draw");
		//TNT1 AAAAAAAA 0 A_Raise();
		TNT1 A 0 A_WeaponOffset(-18.0,68.0);
		ASGN AAAAAAA 1 {
			A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			A_WeaponOffset(2.6,-5.0,WOF_ADD);
			}
		goto ready;
	Deselect.Modern:
		ASGN A 1 A_Lower(12);
		loop;
	Ready.Modern:
		ASGN A 1 BD_WeaponReady();
		goto ready;
	Fire.Modern:
		TNT1 A 0 A_JumpIfNoAmmo("DryReload");
		TNT1 A 0 A_PlaySound("Mweapons/assg/trigger",6);
		ASGN BCD 2;
		ASGN A 2 {
			A_WeaponOffset(0.0,37.0,WOF_INTERPOLATE);
			A_FireBullets (12.0, 8.0, 21, 0, "NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_ShotgunPellet",0,0);
			A_Gunflash("Flash.Modern");
			A_PlaySound("Mweapons/assg/fire"); 
			}
		TNT1 A 0 A_SetPitch(pitch-frandom(2.1,2.5),SPF_INTERPOLATE);
		ASGF B 1 A_WeaponOffset(0.0,39.0,WOF_INTERPOLATE);
		ASGF C 1 A_WeaponOffset(0.0,41.0,WOF_INTERPOLATE);
		ASGF CC 1 A_WeaponOffset(0.0,-1.0,WOF_ADD);
		ASGF BB 1 A_WeaponOffset(0.0,-1.0,WOF_ADD);
		ASGN A 1 A_WeaponOffset(0.0,-1.0,WOF_ADD);
		ASGN A 1 {
			A_WeaponOffset(0.0,-1.0,WOF_ADD);
			If(bdoom_debris==1)
				A_Overlay(10,"BarrelSmoke2.Modern");
			}
		ASGN AAA 1 A_WeaponOffset(0.0,-1.0,WOF_ADD);
		goto ready;
	BarrelSmoke2.Modern:
		TNT1 A 10;
		TNT1 AAAAAAAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	) {
				A_Eject("BarrelSmoke",cos(pitch)*12,0.5,2.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*12,-0.5,2.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				}
			else if (invoker.InStateSequence(s.curstate,invoker.FindState("Reload.Modern"))	) {
				A_Eject("BarrelSmoke",cos(pitch)*12,0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*12,-0.5,3.5,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				}
			}
		stop;
	DryReload:
		ASGN A 18 A_PlaySound("Mweapons/dry");
	Reload.Modern:
		TNT1 A 0 A_JumpIf(CountInv("Shell") == 0, "Ready");
		ASGN ABCD 2 A_WeaponOffset(0.0,1.0,WOF_ADD);
		TNT1 A 0 A_PlaySound("Mweapons/assg/open");
		ASGR ABCCC 3; 
		TNT1 A 0 A_PlaySound("Mweapons/assg/clipout");
		ASGR DEFGHI 2;
		ASGR J 20;
		ASGR ONMLK 2;
		TNT1 A 0 {
			A_PlaySound("Mweapons/assg/clipin");
			A_WeaponOffset(0.0,33.0,WOF_INTERPOLATE);
			}
		ASGR CCCC 2 A_WeaponOffset(0.0,0.75,WOF_ADD);
		TNT1 A 0 A_PlaySound("Mweapons/assg/open");
		ASGR CBA 1;
		ASGN D 5;
		ASGN CBA 1 A_WeaponOffset(0.0,-1.0,WOF_ADD);
		TNT1 A 0 BD_Reload();
		goto ready;
	Flash.Modern:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		ASGN L 2 bright A_Light2();
		TNT1 A 1 bright A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,2,3);
		ASGL A 2 bright A_Light1();
		goto lightdone;
		ASGL B 2 bright A_Light1();
		goto lightdone;
		ASGL C 2 bright A_Light1();
		goto lightdone;
	}
}

Class SSGClip : ammo
{
Default {
inventory.icon "HDRMZ0";
	inventory.amount 1;
	inventory.maxamount 12;
	ammo.backpackamount 0;
	ammo.backpackmaxamount 12;
	}
}