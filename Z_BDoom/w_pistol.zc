Class BD_Pistol : BDoomWeapon replaces Pistol
{
bool wasempty;
Default {
	Obituary "$OB_MPPISTOL";
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NO_AUTO_SWITCH
	Inventory.Pickupmessage "Picked up a pistol";
	Inventory.Pickupsound "weapons/pistol/pickup";
	Tag "$TAG_PISTOL";
	weapon.selectionorder 190;
	inventory.icon "PISTZ0";
	weapon.slotnumber 2;
	weapon.ammotype1 		"Clip";
	weapon.ammouse1 		1;
	weapon.ammogive1	 	0;
	weapon.ammotype2 		"Clip";
	weapon.ammouse2 		1;
	weapon.ammogive2		20;
	inventory.amount		1;
	inventory.maxamount		2;
}
override void Tick()
	{
	if(bdoom_weapons == 1) 
		{ 
		A_SetRenderstyle (1.0,STYLE_Normal); 
		bSPECIAL = true;
		}
	else 
		{ 
		A_SetRenderstyle (1.0,STYLE_None); 
		bSPECIAL = false;
		}
	Super.Tick();
	}
override void DoEffect()
	{
	If (Owner)
		{
		if(bdoom_weapons == 2)
			{
				Ammo1 = ammo(Owner.FindInventory("PistolClip"));
				AmmoUse1 = 1;
				AmmoUse2 = 0;
				bNOAUTOFIRE = true;
				bAMMO_OPTIONAL = true;
			}
		else
			{
				Ammo1 = ammo(Owner.FindInventory("Clip"));
				AmmoUse1 = 1;
				AmmoUse2 = 1;
				bNOAUTOFIRE = false;
				bAMMO_OPTIONAL = false;
			}
		}
	Super.DoEffect();
	}
	
states
	{
	Spawn:
		PIST Z -1;
		stop;
	Ready:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Ready.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Ready.Enhanced");
		TNT1 A 0 A_ClearOverlays(5,5);
		PI0G A 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Deselect:
		TNT1 A 0 A_ClearOverlays(5,5);
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Deselect.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Deselect.Enhanced");
		PI0G A 1 A_Lower();
		wait;
	Select:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Select.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Select.Enhanced");
		PI0S ABCDEE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Fire: //fire 14 tics, refire 5 tics;
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Fire.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Fire.Enhanced");
		PI0F A 3 { 
			A_PlaySound("weapons/pistol");
			A_gunflash("Flash");
			If(Bdoom_tracers)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0);
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		PI0G ABCD 1;
		TNT1 A 0 { 
			if( Bdoom_debris==1)
				A_Eject("PistolCasing",20,1.0,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			}
		PI0G EF 2;
		PI0G A 3 ;
		TNT1 A 0 A_ReFire();
		PI0G A 5 { 
			If( Bdoom_debris==1)
				A_Overlay(10,"BarrelSmoke");
			}
		Goto Ready;
	BarrelSmoke:
		TNT1 A 8;
		TNT1 AAAAAAAAAAAAAAA random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Enhanced"))	||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*12,0,3,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	Flash:
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Flash.Enhanced");
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PI0F L 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PI0L A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	Flash.Enhanced:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIBF L 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIBL A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;

	Ready.Enhanced:
		PIBN A 1 {
			if (CountInv("BD_Pistol") > 1) {
				A_Overlay(5,"Overlay.Dual",0);
				A_WeaponReady();
				}
			else
				A_WeaponReady(WRF_NOSECONDARY);
			}				
		goto ready;
	Select.Enhanced:
		TNT1 A 0 A_PlaySound("weapons/pistol/draw");
		TNT1 A 0 A_JumpIfInventory("BD_Pistol",2,"Select.Dual");
		//TNT1 AAAAAAAA 0 A_Raise();
		PIBS BCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Select.Dual:
		PI2S ABCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Deselect.Enhanced:
		PIBN A 0 {
			let ws = player.findPSprite(PSP_Weapon);
			if (CountInv("BD_Pistol") > 1)
				ws.sprite = GetSpriteIndex("PI2G");
			}
		#### # 1 A_Lower(12);
		wait;
		//load
		PI2G A 0;
		stop;
	Overlay.Dual:
		PI2N A 20;
		stop;
	Fire.Enhanced:
		PIBF A 3 { 
			if (CountInv("BD_Pistol") > 1)
				A_Overlay(5,"Overlay.Dual",0);
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("Flash");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0);
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		PIBN ABCD 1;
		TNT1 A 0 { If( Bdoom_debris==1)
			A_Eject("PistolCasing",20,1.0,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			}
		PIBN EF 2;
		PIBN A 1 ;
		TNT1 A 0 A_ReFire("Fire.Enhanced");
		PIBN A 5 { 
			If( Bdoom_debris==1)
				A_Overlay(10,"BarrelSmoke");
			}
		goto ready;
	AltFire:
		PIBN A 2;
		TNT1 A 0 A_ClearOverlays(5,5);
		PIDC BCD 1;
		PIDF A 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Right");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,7.5); 
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		PIDF BC 1;
		PIDF D 2;
		TNT1 A 0 { 
			If( Bdoom_debris==1)
				A_Eject("PistolCasing",20,6.5,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			}
		TNT1 A 0 A_ReFire();
		PIDE E 2 { 
			If( Bdoom_debris==1)
				A_Overlay(10,"BarrelSmoke");
			}
		PIDF CB 1;
		PIDN A 1;
		PIDC DCB 2;
		goto ready;
	BarrelSmoke2:
		TNT1 A 10;
		TNT1 AAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Enhanced"))	||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*10,-7.5,3,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	AltHold:
		PIDF E 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Left");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,-7.5);
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		PIDF FG 1;
		PIDF H 2;
		TNT1 A 0 { 
			If( Bdoom_debris==1)
				A_Eject("PistolCasing",20,-6.5,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			}
		TNT1 A 0 A_CheckReload();
		PIDF I 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Right");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,7.5);
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		PIDF JK 1;
		PIDF L 2;
		TNT1 A 0 { 
			If( Bdoom_debris==1)
				A_Eject("PistolCasing",20,6.5,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			}
		TNT1 A 0 A_ReFire();
		PIDE E 2 { 
			If( Bdoom_debris==1) {
				A_Overlay(10,"BarrelSmoke");
				A_Overlay(11,"BarrelSmoke2");
				}
			}	
		PIDF CB 1;
		PIDN A 1;
		PIDC DCB 2;
		goto ready;
	AltFlash.Right:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIDF O 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIDL A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	AltFlash.Left:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIDF P 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIDL E 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL F 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL G 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL H 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;

// MODERN GUNS

	Ready.Modern:
		PIBM A 1 {
			A_ClearOverlays(5,5);
			BD_WeaponReady();
			let s = player.FindPSprite (PSP_Weapon);
			if (countinv("PistolClip") < 1) {
				s.sprite = GetSpriteIndex("PIBE");
				s.frame = 2;
				}
			}
		goto ready;
	Select.Modern:
		TNT1 A 0 A_PlaySound("weapons/pistol/draw");
		//TNT1 AAAAAAAA 0 A_Raise;
		PIBS BCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready.Modern;
	Deselect.Modern:
		PIBM A 0 {			
			if (countinv("PistolClip") <= 0) {
				let po = player.FindPSprite (PSP_Weapon);
				po.sprite = GetSpriteIndex("PIBE");
				po.frame = 2;
				}
			}
		#### # 1 A_Lower(12);
		wait;
		PIBE C 0;
		stop;
	Fire.Modern:
		TNT1 A 0 A_JumpIfNoAmmo("DryReload");
		PIMF A 1 {
			A_gunflash("Flash.Modern");
			A_FireBullets(1.9,1.3,-1,0,"NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_PistolBullet",2,0);
			A_PlaySound("Mweapons/pistol/fire",4);
			}
		TNT1 A 0 A_SetPitch(pitch-frandom(0.9,1.3),SPF_INTERPOLATE);
		PIBM ABC 1;
		TNT1 A 0 { 
			If(Bdoom_debris == 1)
				A_Eject("PistolCasing",20,1.0,6.5,frandom(0,2),FRandom(1,3),frandom(4,5));
			A_Overlay(10,"BarrelSmoke");
			if (countinv("PistolClip") < 1)
				return ResolveState("FireEnd.Empty");
			return ResolveState(null);
			}
		PIBM DEEFF 1 A_WeaponReady(WRF_NOSECONDARY);
		goto ready.Modern;
	FireEnd.Empty:
		PIBM DCB 2;
		PIBE C 2;
		goto Ready.Modern;
	Flash.Modern:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PI0F L 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PI0L A 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L B 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L C 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L D 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	DryReload:
		PIBE C 18 A_PlaySound("Mweapons/dry");
	Reload:
		TNT1 A 1 {
			if (CountInv("Clip") == 0)
				return ResolveState("Ready");
			let s = player.FindPSprite (PSP_Weapon);
			If(CountInv("PistolClip") == 0) {
				invoker.wasempty = true;
				s.sprite = GetSpriteIndex("PIBE");
				s.frame = 2;
				}
			else {
				s.sprite = GetSpriteIndex("PIBM");
				s.frame = 0;
				}
			return ResolveState(null);
			}
		#### ##### 1 A_WeaponOffset(1.0,2.0,WOF_ADD);
		#### # 0 A_PlaySound("Mweapons/pistol/clipout",5);
		#### # 19 A_WeaponOffset(0.0,0.0,WOF_ADD);
		#### # 0 {
			A_PlaySound("Mweapons/pistol/clipin",5);
			BD_Reload();
			}
		#### # 1 A_WeaponOffset(0.0,-3.0,WOF_ADD);
		#### ### 1 A_WeaponOffset(0.0,1.0,WOF_ADD);
		#### # 6 A_WeaponOffset(0.0,0.0,WOF_ADD);
		#### ##### 1 A_WeaponOffset(-1.0,-2.0,WOF_ADD);
		TNT1 A 0 {
			if(!invoker.wasempty)
				return ResolveState("Ready");
			A_PlaySound("Mweapons/pistol/cock");
			invoker.wasempty = false;
			return ResolveState(null);
			}
		PIBE CBA 2;
		PIBM A 3;
		TNT1 A 0 BD_Reload();
		goto ready;
	}
}


Class PistolClip : Ammo
{
Default {
inventory.icon "HBULZ0";
inventory.amount 1;
inventory.maxamount 15;
ammo.backpackamount 0;
ammo.backpackmaxamount 15;
	}
}