Class BD_Chaingun : BDoomWeapon replaces Chaingun
{
bool firing;
int HeatStacks;
bool RifleSights;
bool DryReload;
int tfired;
Default
	{
	Tag "$TAG_CHAINGUN";
	weapon.selectionorder 70;
	Weapon.AmmoType2	"Clip";
	Weapon.AmmoGive2	20;
	weapon.slotnumber 4;
	}
override string PickupMessage ()
	{
	if(bdoom_weapons == 2) 
		{
		return "You got the Machine Gun!"; 
		}
	else
		{
		return "$GOTCHAINGUN"; 
		}
	}
override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
    {
	if(bdoom_weapons == 2) 
		{
		return "%o was penetrated by %k's machine gun."; 
		}
	else 
		{
		return "$OB_MPCHAINGUN"; 
		}
	}
override void Tick()
	{
	If (Owner)	
		return;
	else
		{
		if(bdoom_weapons == 2)
			{
			pickupsound = "Mweapons/rifle/clipin";
			AmmoGive1 = 0;
			if (bDROPPED == true)
				AmmoGive2 = 10;
			else
				AmmoGive2 = 20;
			}
		else
			{
			pickupsound = "pickups/chaingun"; 
			AmmoGive2 = 0;
			if (bDROPPED == true)
				AmmoGive1 = 10;
			else
				AmmoGive1 = 20;
			}
		}
	Super.Tick();
	}
override void DoEffect()
	{
	if (owner)
		{
		if(bdoom_weapons == 2)
			{
			Ammo1 = ammo(Owner.FindInventory("RifleClip") );
			ammouse1 = 1;
			Ammo2 = ammo(Owner.FindInventory("Clip"));
			ammouse2 = 0;
			bNOAUTOFIRE = true;
			bAMMO_OPTIONAL = true;
			}
		if(bdoom_weapons == 1)
			{
			Ammo1 = ammo(Owner.FindInventory("Clip"));
			ammouse1 = 1;
			Ammo2 = ammo(Owner.FindInventory("Clip"));
			ammouse2 = 1;
			bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = false;
			}
		if(bdoom_weapons == 0)
			{
			Ammo1 = ammo(Owner.FindInventory("Clip"));
			ammouse1 = 1;
			Ammo2 = null;
			ammouse2 = 0;
			bNOAUTOFIRE = false;
			bAMMO_OPTIONAL = false;
			}
		}
	Super.DoEffect();
	}
states
	{
	Spawn:
		MGUN A 1 NoDelay A_JumpIf(bdoom_weapons == 2,1);
		loop;
		RIFN Z 1;
		loop;
	Select:
		TNT1 A 0 A_StopSound(5);
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Select.Modern");
		TNT1 A 0 { 
			If(bdoom_weapons == 1) 
				A_PlaySound("weapons/chaingun/draw");
			}
		//TNT1 AAAAAAAA 0 A_Raise();
		MGGS ABCDEF 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Deselect:
		TNT1 A 0 { 
			invoker.firing = 0;
			invoker.tfired = 0;
			invoker.HeatStacks = 0; 
			A_StopSound(5); 
			}
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Deselect.Modern");
		TNT1 A 0 { 
			A_SetSpeed(1);
			A_ZoomFactor(1.0);
			invoker.HeatStacks = 0; 
			}
		MGGG A 1 A_Lower();
		wait;
	Ready:
		TNT1 A 0  { invoker.tfired = 0; }
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Ready.Modern");
		MGGG A 1 
			{
			invoker.HeatStacks = 0;
			A_SetSpeed(1);
			A_ZoomFactor(1.0); 
			if (bdoom_weapons == 0)
				A_WeaponReady(WRF_NOSECONDARY);
			else 
				A_WeaponReady();
			}
		loop;
	BarrelSmoke:
		TNT1 A 12;
		TNT1 AAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*8,0,2,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	BarrelSmokeFull:
		TNT1 A 4;
		TNT1 AAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	) {
				A_Eject("BarrelSmoke",cos(pitch)*8,0,	2,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*8,1.2,	3,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*8,-1.2,3,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				A_Eject("BarrelSmoke",cos(pitch)*8,0,	4,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
				}
			}
		stop;
	Fire:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Fire.Modern");
		MGGG A 1 {
			A_WeaponOffset(0.0,35.0,WOF_INTERPOLATE);
			a_gunflash();
			if (bdoom_weapons == 0)
				A_PlaySound("weapons/chngun");
			else 
				A_PlaySound("weapons/chaingun/fire");
			If(Bdoom_tracers==1)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0) ;
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		MGGG B 1;
		TNT1 A 0 { 
			if( Bdoom_debris==1)
				A_Eject("ChaingunCasing",20,frandom(7,8),10,frandom(0,2),FRandom(3.5,4.5),frandom(4,5));
			}
		MGGG CD 1;
		TNT1 A 0 A_CheckReload();
		
		MGGG A 1 {
			a_gunflash();
			if (bdoom_weapons == 0)
				A_PlaySound("weapons/chngun");
			else 
				A_PlaySound("weapons/chaingun/fire");
			If(Bdoom_tracers==1)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0) ;
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
			}
		MGGG B 1;
		TNT1 A 0 { 
			if( Bdoom_debris==1)
				A_Eject("ChaingunCasing",20,frandom(7,8),10,frandom(0,2),FRandom(3.5,4.5),frandom(4,5));
			}
		MGGG CD 1;
		TNT1 A 0 A_refire();
		TNT1 A 0 { 
			if (Bdoom_debris == 1)
				A_Overlay(20,"BarrelSmoke");
			}
		MGGG ABCD 1 {
			A_WeaponReady(WRF_NOSECONDARY);
			A_WeaponOffset(0.0,-0.75,WOF_ADD);
			}
		MGGG ABCDAABBCCDD 1 A_WeaponReady(WRF_NOSECONDARY);
		goto ready;
	Flash:
		TNT1 A 0 { 
			if (invoker.HeatStacks > 55) {
				A_Overlay(10,"FlashHeated");
				return ResolveState(1); }
			if (invoker.HeatStacks > 50) {
				A_Overlay(9,"FlashHeated");
				return ResolveState(1); }
			if (invoker.HeatStacks > 45) {
				A_Overlay(8,"FlashHeated");
				return ResolveState(1); }
			if (invoker.HeatStacks > 40) {
				A_Overlay(7,"FlashHeated");
				return ResolveState(1); }
			if (invoker.HeatStacks > 35) {
				A_Overlay(6,"FlashHeated");
				return ResolveState(1); }
			if (invoker.HeatStacks > 30) {
				A_Overlay(5,"FlashHeated");
				return ResolveState(1); }
			return ResolveState(null);
				}
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,2);
		MGGF L 1 bright  A_Light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6);
		MGGL A 1 bright  A_Light1();
		goto lightdone;
		MGGL B 1 bright  A_Light1();
		goto lightdone;
		MGGL C 1 bright  A_Light1();
		goto lightdone;
		MGGL D 1 bright  A_Light1();
		goto lightdone;
		MGGL E 1 bright  A_Light1();
		goto lightdone;
		MGGL F 1 bright  A_Light1();
		goto lightdone;
	FlashHeated:
		MGGH A 40 bright;
		TNT1 A 0 {
			if (invoker.firing == 1)
				return ResolveState("FlashHeated");
			return ResolveState(null);
			}
		stop;
	AltFire:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"AltFire.Modern");
		TNT1 A 0 A_playsound("weapons/chaingun/start");
		MGGG ABCDABCD 2;
		MGGG ABCDABCD 1;
		TNT1 A 0 A_playsound("weapons/chaingun/spin",5,1.0,true);
		MGGG ACACACACAC 1;
		
		MGGG A 1 { 
				invoker.firing = 1;
				A_WEaponOffset(0.0,35.0,WOF_INTERPOLATE);
				invoker.HeatStacks++; 
				a_gunflash();
				A_PlaySound("weapons/chaingun/fire");
				If(Bdoom_tracers==1)
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0) ;
				else
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
				}
		MGGG C 1 { 
			if( Bdoom_debris==1)
				A_Eject("ChaingunCasing",20,frandom(7,8),10,frandom(0,2),FRandom(3.5,4.5),frandom(4,5));
				}
		TNT1 A 0 A_CheckReload();		
		MGGG A 1 { 
				invoker.HeatStacks++; 
				a_gunflash();
				A_PlaySound("weapons/chaingun/fire");
				If(Bdoom_tracers==1)
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0) ;
				else
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
				}
		MGGG C 1 { 
			if( Bdoom_debris==1)
				A_Eject("ChaingunCasing",20,frandom(7,8),10,frandom(0,2),FRandom(3.5,4.5),frandom(4,5));
			}
	AltHold:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"AltFire.Modern");
		TNT1 A 0 A_CheckReload();
		MGGG A 1 { 
				A_WEaponOffset(0.0,35.0,WOF_INTERPOLATE);
				invoker.HeatStacks++; 
				a_gunflash();
				A_PlaySound("weapons/chaingun/fire");
				If(Bdoom_tracers==1)
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BulletTracer",0,0) ;
				else
					A_FireBullets(8,3, -1, 5, "EnBulletPuff",FBF_NORANDOMPUFFZ);
				}	
		MGGG C 1 { 
				if (Bdoom_debris == 1)
					A_Eject("ChaingunCasing",20,frandom(7,8),10,frandom(0,2),FRandom(3.5,4.5),frandom(4,5));
				}
		TNT1 A 0 {
			if (invoker.HeatStacks < 60)
				A_ReFire("AltHold"); 
			invoker.firing = 0;
			}
		TNT1 A 0 A_playsound("weapons/chaingun/stop",5);
		MGGG ABCDA 1 {
			A_WeaponOffset(0.0,-0.6,WOF_ADD);
			If(bdoom_debris && invoker.HeatStacks >= 60)
				A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-3,3),sin(-pitch)*25+random(33,37),		0,0,frandom(0.2,0.4),0);
			}
		MGGG BCDABC 2 {
			If(bdoom_debris && invoker.HeatStacks >= 60)
				A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-3,3),sin(-pitch)*25+random(33,37),		0,0,frandom(0.2,0.4),0);
			}
		MGGG DABCD 3 {
			If(bdoom_debris && invoker.HeatStacks >= 60)
				A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-3,3),sin(-pitch)*25+random(33,37),		0,0,frandom(0.2,0.4),0);
			}
		TNT1 A 0 {
			If(bdoom_debris)
				A_Overlay(20,"BarrelSmokeFull");
			}
		TNT1 A 0 {
			A_ClearRefire();
			if (invoker.HeatStacks < 60)
				return ResolveState("Ready");
			return ResolveState(null);
			}
		MGGG ABCD 4 {
			If(bdoom_debris==1)
				A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-3,3),sin(-pitch)*25+random(33,37),		0,0,frandom(0.2,0.4),0);
			A_WeaponReady(WRF_NOFIRE);
			}
		TNT1 A 0 {
			If(bdoom_debris)
				A_Overlay(20,"BarrelSmokeFull");
			}
		goto ready;

	//MODERN
	Select.Modern:
		//TNT1 AAAAAAAA 0 A_Raise();
		TNT1 A 0 {
			A_PlaySound("MWeapons/rifle/select");
			A_Weaponoffset(30.0,67.0);
			}
		TNT1 A 0 A_jumpIfNoammo(8);
		RIFN CCCCCBA 1 {
			A_WeaponOffset(-4.0,-5.0,WOF_ADD);
			A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			}
		goto ready;
		RIFN DDDDDDD 1 {
			A_WeaponOffset(-4.0,-5.0,WOF_ADD);
			A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
			}
		goto ready;
	Deselect.Modern:
		TNT1 A 0 A_JumpIf(invoker.RifleSights != 1,"Deselect1.Modern");
		TNT1 A 0 { 
			invoker.RifleSights = false; 
			A_SetSpeed(1);
			A_ZoomFactor(1.0);
			}
		RIFA CBA 3;
	Deselect1.Modern:
		RIFN A 1 {			
			if (countinv("RifleClip") <= 0) {
				let po = player.FindPSprite (PSP_Weapon);
				po.frame = 3;
				}
			}
		#### # 1 A_Lower(12);
		wait;
		RIFN D 0;
		stop;
	Ready.Modern:
		RIFN A 1 {
			let s = player.findPSprite(PSP_Weapon);
			if (invoker.riflesights) 
				return ResolveState("ReadySights");	
			else if (Countinv("RifleClip") < 1)
				s.frame = 3;
			BD_WeaponReady(1);
			return ResolveState(null);
			}
		goto ready;
	ReadySights:
		RIFS A 1 {
			BD_WeaponReady(1);
			A_SetSpeed(0.8);
			A_ZoomFactor(1.3);
			}
		goto ready;		
	BarrelSmoke.Modern:
		TNT1 A 8;
		TNT1 AAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("Ready"))			||
				invoker.InStateSequence(s.curstate,invoker.FindState("Ready.Modern"))	)
				A_Eject("BarrelSmoke",cos(pitch)*14,0,3,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	BarrelSmoke.Sights:
		TNT1 A 8;
		TNT1 AAAAAAAAAAAAAAA 	random(1,2) {
			let s = player.GetPSprite(PSP_Weapon);
			if (invoker.InStateSequence(s.curstate,invoker.FindState("ReadySights")))
				A_Eject("BarrelSmoke",cos(pitch)*7,0,1,	frandom(0.015,-0.015),frandom(0.015,-0.015),0.1);
			}
		stop;
	Fire.Modern:
		TNT1 A 0 A_JumpIfNoAmmo("DryReload");
		TNT1 A 0 A_JumpIf (invoker.RifleSights == true,"FireSights");
		RIFN A 1 {
			A_gunflash("Flash.Modern");
			A_PlaySound("Mweapons/rifle/fire");
			A_WeaponOffset(0.0,35.0,WOF_INTERPOLATE);
			A_FireBullets(1.5 + (invoker.tfired * 0.15),1.0 + (invoker.tfired * 0.15),-1,0,"NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
			invoker.tfired++;
			if (invoker.tfired > 9)
				invoker.tfired = 9;
			}
		RIFN C 1 {
			A_WeaponOffset(frandom(-0.8,0.8),34.0,WOF_INTERPOLATE);
			A_SetPitch(pitch - frandom(0.37,0.42),SPF_INTERPOLATE);
			}
		
		RIFN CB 1 A_WeaponOffset(frandom(-0.4,0.4),33.0,WOF_INTERPOLATE);
		RIFN B 1 A_WeaponOffset(0,32.0,WOF_INTERPOLATE);
		
		TNT1 A 0 {
			If(Bdoom_debris == 1) {
				A_Eject("ChaingunCasing",20,frandom(5,6),9,frandom(0,2),FRandom(5.5,7),frandom(2,2.5));
				A_overlay(20,"BarrelSmoke.Modern");
				}
			}
		TNT1 A 0 A_ReFire("Fire.Modern");
		TNT1 A 0 A_JumpIfNoAmmo(2);
		RIFN A 2;
		goto ready;
		RIFN D 2;
		goto ready;
	FireSights:
		RIFS A 1 {
			A_WeaponOffset(0.0,34.0,WOF_INTERPOLATE);
			A_FireBullets(0.7,0.35,-1,0,"NullPuff",FBF_USEAMMO|FBF_NORANDOMPUFFZ,0,"BD_RifleBullet",0,0);
			A_gunflash("FlashSights");
			A_PlaySound("Mweapons/rifle/fire");
			}
		RIFS C 1 A_SetPitch(pitch - 0.3,SPF_INTERPOLATE);
		TNT1 A 0 { 
			If(Bdoom_debris == 1) {
				A_Eject("ChaingunCasing",17,frandom(6,7),6,frandom(0,2),FRandom(5.5,7),frandom(2,2.5));
				A_overlay(20,"BarrelSmoke.Sights");
				}
			}
		RIFS C 1;
		RIFS B 1 A_WeaponOffset(0.0,34.0,WOF_INTERPOLATE);
		RIFS B 1 A_WeaponOffset(0.0,32.0,WOF_INTERPOLATE);
		RIFS A 1 A_WeaponOffset(0.0,34.0,WOF_INTERPOLATE);
		RIFS A 1 A_WeaponOffset(0.0,32.0,WOF_INTERPOLATE);
		goto ready;
	DryReload:
		TNT1 A 0 { invoker.DryReload = 1; }
		TNT1 A 0 A_JumpIf (invoker.RifleSights == true,2);
		RIFN D 18 A_PlaySound("Mweapons/dry",7);
		goto reload;
		RIFS A 18 A_PlaySound("Mweapons/dry",7);
	Reload:
		TNT1 A 0 { 
			if (CountINv("Clip") == 0)
				return ResolveState("Ready");
			if (invoker.RifleSights == false)
				Return ResolveState("ReloadDo");
			else
				{
				invoker.RifleSights = false;
				A_SetSpeed(1);
				A_ZoomFactor(1.0);
				}
			return ResolveState(null);
			}
		RIFA CBA 3;
	ReloadDo:
		RIFR ABCDE 2;
		RIFR F 8 A_PlaySound("Mweapons/rifle/clipout");
		RIFR GH 2;
		RIFR I 5;
		RIFR JKL 2;
		TNT1 A 0 BD_Reload();
		RIFR M 1 A_PlaySound("Mweapons/rifle/clipin");
		RIFR M 1 A_WeaponOffset(1.0,32.0,WOF_INTERPOLATE);
		RIFR M 1 A_WeaponOffset(2.0,32.0,WOF_INTERPOLATE);
		RIFR M 1 A_WeaponOffset(3.0,32.0,WOF_INTERPOLATE);
		RIFR M 1 A_WeaponOffset(1.0,32.0,WOF_INTERPOLATE);
		RIFR NHGFEDC 2;
		TNT1 A 0 A_JumpIf(invoker.DryReload != 1,6);
		TNT1 A 0 { 
			A_PlaySound("Mweapons/rifle/cock");
			invoker.DryReload = 0;
			}
		RIFN DEAA 2;
		goto ready;
		RIFR BA 2;
		goto ready;
	AltFire.Modern:
		TNT1 A 0 A_JumpIf (invoker.RifleSights == true,"UnsetSights");
		TNT1 A 0 { invoker.RifleSights = true; 
			A_PlaySound("Mweapons/rifle/aim",7);
			A_SetSpeed(0.8);
			A_ZoomFactor(1.3);
			}
		RIFA ABC 3;
		goto ReadySights;
	UnsetSights:
		TNT1 A 0 { 
			invoker.RifleSights = false;	
			A_PlaySound("Mweapons/rifle/unaim",7);
			A_SetSpeed(1);
			A_ZoomFactor(1.0);
			}
		RIFA CBA 3;
		goto Ready;
	Flash.Modern:
		TNT1 A 0 A_Jump(256,1,3,5,7);
		RIFL A 1 bright offset(0,34) A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL B 1 bright offset(0,34) A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL C 1 bright offset(0,34) A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL D 1 bright offset(0,34) A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	FlashSights:
		TNT1 A 0 A_Jump(256,1,3,5,7);
		RIFL E 1 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL F 1 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL G 1 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		RIFL H 1 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	}
}

Class RifleClip : ammo
{
Default {
inventory.icon "HMAGZ0";
	inventory.amount 1;
	inventory.maxamount 40;
	ammo.backpackamount 0;
	ammo.backpackmaxamount 40;
	}
}